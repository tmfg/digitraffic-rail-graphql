plugins {
    id 'org.springframework.boot' version '2.3.3.RELEASE'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id 'java'
    id "io.github.kobylynskyi.graphql.codegen" version "1.2.2"
}

group = 'fi.digitraffic'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

def queryDslVersion = '4.1.3'

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.graphql-java-kickstart:graphiql-spring-boot-starter:5.10.0'
    compile 'com.graphql-java:graphql-java-extended-scalars:15.0.0'
    implementation 'com.graphql-java:graphql-java:15.0'
    implementation 'com.graphql-java:graphql-java-spring-boot-starter-webmvc:2.0'

    compile 'com.vividsolutions:jts:1.13'
    compile('org.hibernate:hibernate-spatial') {
        exclude group: 'postgresql', module: 'org.postgresql'
    }
    runtimeOnly 'mysql:mysql-connector-java'
    compile 'technology.dice.open:dice-fairlink:1.2.4'
    compile("com.querydsl:querydsl-core:${queryDslVersion}")
    compile("com.querydsl:querydsl-jpa:${queryDslVersion}")
    compile 'javax.inject:javax.inject:1'

    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    implementation 'net.logstash.logback:logstash-logback-encoder:5.1'

    compile 'com.google.guava:guava:31.0.1-jre'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

dependencies {
    compile "com.querydsl:querydsl-jpa:${queryDslVersion}"
    annotationProcessor (
            "com.querydsl:querydsl-apt:${queryDslVersion}:jpa",
            "org.hibernate.javax.persistence:hibernate-jpa-2.1-api:1.0.2.Final",
            "javax.annotation:javax.annotation-api:1.3.2",
    )
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', 'src/main/graphqlGen']
        }
    }
}

task cleanGraphqlGen {
    doFirst {
        delete("$projectDir/src/main/graphqlGen")
    }
}

clean {
    dependsOn 'cleanGraphqlGen'
}

compileJava.dependsOn graphqlCodegen

graphqlCodegen {
    graphqlSchemaPaths = ["$projectDir/src/main/resources/schema.graphqls".toString()]
    outputDir = new File("$projectDir/src/main/graphqlGen")
    apiPackageName = "fi.digitraffic.graphql.rail.api"
    modelPackageName = "fi.digitraffic.graphql.rail.model"
    customTypesMapping = [
            DateTime: "java.time.ZonedDateTime",
            Date    : "java.time.LocalDate"
    ]
    modelNameSuffix = "TO"
    generateApis = true
}

test {
    useJUnitPlatform()
}

