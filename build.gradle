buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.owasp:dependency-check-gradle:8.1.2'
    }
}

plugins {
    id 'org.springframework.boot' version '2.7.8'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
    id 'java'
    id "io.github.kobylynskyi.graphql.codegen" version "1.2.2"
}

apply plugin: 'org.owasp.dependencycheck'

dependencyCheck {
    failBuildOnCVSS = 7
    suppressionFile = 'owasp-suppressions.xml'
    analyzers {
        ossIndex {
            // OSS Index can be unstable
            warnOnlyOnRemoteErrors = true
        }
    }
}

group = 'fi.digitraffic'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

def queryDslVersion = '5.0.0'

repositories {
    mavenCentral()
}

dependencies {
    /* Checkmarx falsely identifies graphql-java:18.3 as vulnerable to CVE-2022-37734
       18.3 is the fixed version. See https://nvd.nist.gov/vuln/detail/CVE-2022-37734 */
    implementation 'com.graphql-java:graphql-java:18.3'
    implementation 'com.graphql-java-kickstart:graphiql-spring-boot-starter:11.1.0'
    implementation 'com.graphql-java:graphql-java-extended-scalars:19.0'
    implementation 'com.graphql-java:graphql-java-spring-boot-starter-webmvc:2.0'

    implementation 'com.vividsolutions:jts:1.13'
    implementation 'org.springframework.cloud:spring-cloud-starter-bootstrap:3.1.6'
    implementation 'io.awspring.cloud:spring-cloud-starter-aws-secrets-manager-config:2.4.3'
    implementation('org.hibernate:hibernate-spatial') {
        exclude group: 'org.postgresql', module: 'postgresql'
    }
    runtimeOnly 'com.mysql:mysql-connector-j:8.0.32'
    implementation 'technology.dice.open:dice-fairlink:1.2.4'
    implementation "com.querydsl:querydsl-core:${queryDslVersion}"
    implementation "com.querydsl:querydsl-jpa:${queryDslVersion}"
    implementation 'javax.inject:javax.inject:1'

    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    implementation 'net.logstash.logback:logstash-logback-encoder:5.1'

    // QueryDSL v5 removed dependency to guava. We are using some functionality from
    // com.google.common.base that guava provides.
    implementation 'com.google.guava:guava:31.1-jre'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }

    annotationProcessor (
            "com.querydsl:querydsl-apt:${queryDslVersion}:jpa",
            "org.hibernate.javax.persistence:hibernate-jpa-2.1-api:1.0.2.Final",
            "javax.annotation:javax.annotation-api:1.3.2",
    )
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', 'src/main/graphqlGen']
        }
    }
}

task cleanGraphqlGen {
    doFirst {
        delete("$projectDir/src/main/graphqlGen")
    }
}

clean {
    dependsOn 'cleanGraphqlGen'
}

compileJava.dependsOn graphqlCodegen

graphqlCodegen {
    graphqlSchemaPaths = ["$projectDir/src/main/resources/schema.graphqls".toString()]
    outputDir = new File("$projectDir/src/main/graphqlGen")
    apiPackageName = "fi.digitraffic.graphql.rail.api"
    modelPackageName = "fi.digitraffic.graphql.rail.model"
    customTypesMapping = [
            DateTime: "java.time.ZonedDateTime",
            Date    : "java.time.LocalDate"
    ]
    modelNameSuffix = "TO"
    generateApis = true
}

test {
    useJUnitPlatform()
}
